plugins {
	id 'application'
	id 'com.github.johnrengelman.shadow'
}

def flatLafNatives = [
		"windows-arm64@dll",
		"windows-x86@dll",
		"windows-x86_64@dll",
		"linux-x86_64@so",
]

dependencies {
	implementation project(':enigma')
	implementation project(':enigma-server')

	implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
	implementation 'com.formdev:flatlaf:3.2.5'
	implementation 'com.formdev:flatlaf-extras:3.2.5' // for SVG icons
	implementation 'de.sciss:syntaxpane:1.2.1'
	implementation 'com.github.lukeu:swing-dpi:0.10'
	implementation 'org.drjekyll:fontchooser:2.5.2'

	flatLafNatives.forEach {
		implementation 'com.formdev:flatlaf:3.2.5:' + it
	}
}

application {
	mainClass = 'cuchaz.enigma.gui.Main'
}

jar.manifest.attributes 'Main-Class': application.mainClass.get()

publishing {
	publications {
		shadow(MavenPublication) { publication ->
			publication.from components.java
		}
	}
}