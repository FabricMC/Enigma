plugins {
	id 'application'
	id 'com.github.johnrengelman.shadow'
}

def flatLafNatives = [
		"windows-arm64@dll",
		"windows-x86@dll",
		"windows-x86_64@dll",
		"linux-x86_64@so",
]

dependencies {
	implementation project(':enigma')
	implementation project(':enigma-server')

	implementation "net.sf.jopt-simple:jopt-simple:${jopt_simple_version}"
	implementation "com.formdev:flatlaf:${flatlaf_version}"
	implementation "com.formdev:flatlaf-extras:${flatlaf_version}" // for SVG icons
	implementation "de.sciss:syntaxpane:${syntaxpane_version}"
	implementation "com.github.lukeu:swing-dpi:${swing_dpi_version}"
	implementation "org.drjekyll:fontchooser:${fontchooser_version}"

	flatLafNatives.forEach {
		implementation "com.formdev:flatlaf:${flatlaf_version}:" + it
	}
}

application {
	mainClass = 'cuchaz.enigma.gui.Main'
}

jar.manifest.attributes 'Main-Class': application.mainClass.get()

publishing {
	publications {
		shadow(MavenPublication) { publication ->
			publication.from components.java
		}
	}
}
