plugins {
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'checkstyle'

	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://maven.fabricmc.net/' }
	}

	dependencies {
		implementation "com.google.guava:guava:${guava_version}-jre"
		implementation "com.google.code.gson:gson:${gson_version}"
		implementation "net.fabricmc:mapping-io:${mappingio_version}"

		compileOnly "org.jetbrains:annotations:${jetbrains_annotations_version}"

		testImplementation "junit:junit:${junit_version}"
		testImplementation "org.hamcrest:hamcrest:${hamcrest_version}"
	}

	group = rootProject.group
	version = rootProject.version

	version = version + (System.getenv("GITHUB_ACTIONS") ? "" : "+local")

	java {
		withSourcesJar()
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"

		it.options.release = 17
	}

	checkstyle {
		configFile = rootProject.file('checkstyle.xml')
		toolVersion = rootProject.checkstyle_version
	}

	publishing {
		publications {
			"$project.name"(MavenPublication) {
				groupId project.group
				artifactId project.name
				version project.version
				from components.java
			}
		}
	}
}

allprojects {
	publishing {
		repositories {
			mavenLocal()

			def ENV = System.getenv()
			if (ENV.MAVEN_URL) {
				maven {
					url ENV.MAVEN_URL
					credentials {
						username ENV.MAVEN_USERNAME
						password ENV.MAVEN_PASSWORD
					}
				}
			}
		}
	}
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def xml = new URL("https://maven.fabricmc.net/cuchaz/enigma/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}

publish.mustRunAfter checkVersion
